apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

def nativeLib(buildDir, projectDir, service, osname) {
    return tasks.create("nativeLib${service}${osname}") {
        doLast {
            File lib = nativeBuild("$buildDir", "$projectDir", "$service", "$osname")
            if (lib != null) {
                tasks["jar${service}${osname}"].configure {
                    into("native") {
                        from lib
                    }
                }
            }
        }
    }
}

def jarPackage(buildDir, projectDir, service, osname) {
    return tasks.create("jar${service}${osname}", Jar) {
        if (osname != "") {
            def nativeTask = nativeLib("$buildDir", "$projectDir", "$service", "$osname")
            dependsOn(nativeTask)
            archiveBaseName = "${service}".toLowerCase()
            archiveClassifier = osname
            into ("") {
                from "${buildDir}/classes/java/main"
                if (osname == 'desktop') {
                    exclude '**/*/impl/Dummy**', "**/impl/IOS**"
                } else {
                    exclude '**/*/impl/Dummy**', "**/impl/Desktop**"
                }
            }
        } else {
            from "${buildDir}/classes/java/main"
            exclude "**/impl/Desktop**", "**/impl/IOS**"
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            def service = getServiceName(project.name)
            if (service != "Util") {
                artifact jarPackage("$project.buildDir", "$project.projectDir", "${service}", "ios")
                artifact jarPackage("$project.buildDir", "$project.projectDir", "${service}", "desktop")
            }
            artifact jarPackage("$project.buildDir", "$project.projectDir", "${service}", "")
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "Gluon Attach (${project.name})"
                description = "API for accessing ${project.name} features"
                url = 'https://gluonhq.com/products/mobile/'

                developers {
                    developer {
                        name = 'Johan Vos'
                        email = 'johan.vos@gluonhq.com'
                        organization = 'Gluon'
                        organizationUrl = 'https://gluonhq.com'
                    }
                    developer {
                        name = 'Joeri Sykora'
                        email = 'joeri.sykora@gluonhq.com'
                        organization = 'Gluon'
                        organizationUrl = 'https://gluonhq.com'
                    }
                    developer {
                        name = 'Erwin Morrhey'
                        email = 'erwin.morrhey@gluonhq.com'
                        organization = 'Gluon'
                        organizationUrl = 'https://gluonhq.com'
                    }
                }

                licenses {
                    license {
                        name = 'The GNU General Public License'
                        url = 'https://opensource.org/licenses/GPL-3.0'
                        distribution = 'repo'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/tiainen/attach.git'
                    developerConnection = 'scm:git:ssh://git@github.com/tiainen/attach.git'
                    url = 'https://github.com/tiainen/attach'
                }
            }

            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    it.groupId.text() == 'org.openjfx'
                }.each {
                    it.remove(it.classifier)
                }
            }
        }
    }

    repositories {
        maven {
            // to upload release, use:
            // ./gradlew -PrepositoryUrl=https://oss.sonatype.org/service/local/staging/deploy/maven2 publish
//            url = getStringProperty('repositoryUrl', 'https://oss.sonatype.org/content/repositories/snapshots/')
            url = getStringProperty('repositoryUrl', 'https://nexus.gluonhq.com/nexus/content/repositories/public-snapshots/')
            credentials {
                username = getStringPropertyDefaultAsProperty('repositoryUsername', 'gluonNexusUsername')
                password = getStringPropertyDefaultAsProperty('repositoryPassword', 'gluonNexusPassword')
            }
        }
    }
}

signing {
    sign publishing.publications.maven
    required = { gradle.taskGraph.hasTask(publish) && !version.endsWith("SNAPSHOT") }
}

private String getServiceName(projectName) {
    if (projectName.contains("-")) { // in-app-billing -> InAppBilling
        def values = projectName.tokenize("-")
        return values.inject("") { name, pn -> name + pn.capitalize()  }
    }
    return projectName.capitalize()
}

private String getStringProperty(String name, String defaultValue) {
    if (!project.hasProperty(name)) {
        return defaultValue
    }

    def propertyValue = project.property(name)
    return propertyValue != null ? propertyValue.toString().trim() : defaultValue
}

private String getStringPropertyDefaultAsProperty(String name, String defaultProperty) {
    if (!project.hasProperty(name)) {
        return project.hasProperty(defaultProperty) ? project.property(defaultProperty) : System.getenv(name)
    }

    def propertyValue = project.property(name)
    if (propertyValue != null) {
        return propertyValue.toString().trim()
    } else {
        return project.hasProperty(defaultProperty) ? project.property(defaultProperty) : System.getenv(name)
    }
}
