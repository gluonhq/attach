import java.time.Duration

plugins {
    id 'java-library'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id "org.openjfx.javafxplugin" version "0.1.0" apply false
}

if (System.getProperty('os.name').toLowerCase().contains("mac")) {
    new ByteArrayOutputStream().withStream { os ->
        exec {
            args '-version', '-sdk', 'iphoneos', 'SDKVersion'
            executable 'xcodebuild'
            standardOutput = os
        }
        ext.IOS_VERSION = os.toString().trim()
        println "IOS version: $IOS_VERSION"
    }
} else {
    ext.IOS_VERSION = ""
}

subprojects {
    apply plugin: "org.openjfx.javafxplugin"
    apply from: rootProject.file('gradle/mavenPublish.gradle')

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
    }

    javafx {
        version = "20.0.2"
        modules = [ 'javafx.graphics', 'javafx.media']
    }
}

nexusPublishing {
    packageGroup = 'com.gluonhq'
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username = project.hasProperty('sonatypeUsername') ? project.property('sonatypeUsername') : ''
            password = project.hasProperty('sonatypePassword') ? project.property('sonatypePassword') : ''
        }
    }
    clientTimeout = Duration.ofMinutes(5)
    connectTimeout = Duration.ofMinutes(5)
    transitionCheckOptions {
        maxRetries.set(50)
        delayBetween.set(Duration.ofSeconds(10))
    }
}

task copyModuleSources {
    doFirst {
        subprojects.each {p ->
            copy {
                from p.sourceSets.main.java
                into "${buildDir}/modules/sources/${p.ext.moduleName}"
            }
        }
    }
}

javadoc {
    doFirst {
        subprojects.each {p -> classpath += p.sourceSets.main.compileClasspath }
        options.modulePath = new ArrayList(classpath.files)
    }
    exclude("com/gluonhq/attach/*/impl")
    source fileTree("${buildDir}/modules/sources")
    options.addStringOption("-module-source-path", "$buildDir/modules/sources")
    classpath = files()
    title "Attach ${project.version} API"
    dependsOn copyModuleSources
}

// Do not publish Gradle modules
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}