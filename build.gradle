plugins {
    id 'io.codearte.nexus-staging' version '0.21.1'
    id 'de.marcphilipp.nexus-publish' version '0.4.0' apply false
    id "org.openjfx.javafxplugin" version "0.0.8" apply false
}

apply from: rootProject.file("gradle/native-build.gradle")

if (System.getProperty('os.name').toLowerCase().contains("mac")) {
    new ByteArrayOutputStream().withStream { os ->
        exec {
            args '-version', '-sdk', 'iphoneos', 'SDKVersion'
            executable 'xcodebuild'
            standardOutput = os
        }
        ext.IOS_VERSION = os.toString().trim()
        println "IOS version: $IOS_VERSION"
    }
} else {
    ext.IOS_VERSION = ""
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "org.openjfx.javafxplugin"
    apply from: rootProject.file('gradle/mavenPublish.gradle')

    sourceCompatibility = 11
    targetCompatibility = 11

    configurations {
        androidImplementation
    }

    repositories {
        mavenCentral()
    }
}

nexusStaging {
    packageGroup = 'com.gluonhq'
    username = project.hasProperty('sonatypeUsername') ? project.property('sonatypeUsername') : ''
    password = project.hasProperty('sonatypePassword') ? project.property('sonatypePassword') : ''
    numberOfRetries = 30
    delayBetweenRetriesInMillis = 5000
}